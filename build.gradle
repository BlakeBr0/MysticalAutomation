plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.83'
}

version = '1.21.1-1.0.2'
group = 'com.blakebr0.mysticalautomation'

base {
    archivesName = 'MysticalAutomation'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

repositories {
    mavenLocal()
    maven { url 'https://maven.blakesmods.com' }
    maven { url 'https://maven.blamejared.com' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

tasks.register('syncTestDatapacks', Sync) {
    if (file('run/saves').exists()) {
        file('run/saves').eachDir { dir ->
            project.sync {
                from 'src/test/resources/datapacks'
                into file(dir.path + '/datapacks')
            }
        }
    }

    project.sync {
        from 'src/test/resources/datapacks'
        into file('run/world/datapacks')
    }
}

tasks.register('syncTestScripts', Sync) {
    from 'src/test/resources/scripts'
    into 'run/scripts'
}

neoForge {
    version = neo_version

    parchment {
        mappingsVersion = parchment_mappings_version
        minecraftVersion = parchment_minecraft_version
    }

    runs {
        client {
            client()
        }

        server {
            server()
        }

        gameTestServer {
            type = "gameTestServer"
        }

        data {
            data()

            // programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'terminal.jline', 'true'
            logLevel = org.slf4j.event.Level.DEBUG
            ideName = "MysticalAutomation - ${it.ideName.get()}"
            taskBefore tasks.named('syncTestDatapacks')
            taskBefore tasks.named('syncTestScripts')
        }
    }

    mods {
        mysticalautomation {
            sourceSet(sourceSets.main)
        }
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "com.blakebr0.cucumber:Cucumber:${cucumber_version}"
    implementation "com.blakebr0.mysticalagriculture:MysticalAgriculture:${mysticalagriculture_version}"
    implementation "com.blakebr0.mysticalagradditions:MysticalAgradditions:${mysticalagradditions_version}"
    implementation "com.blamejared.crafttweaker:CraftTweaker-neoforge-${crafttweaker_version}"

    runtimeOnly "curse.maven:jade-324717:${jade_version}"
    runtimeOnly "vazkii.patchouli:Patchouli:${patchouli_version}"
    runtimeOnly "mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}"

    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            mod_version                  : version.split('-').last(),
            cucumber_version             : cucumber_version.split('-').last(),
            mysticalagriculture_version  : mysticalagriculture_version.split('-').last(),
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
